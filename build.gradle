/*
 * Copyright (c) 2007-2013 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    mavenRepo name: 'conjars', url: 'http://conjars.org/repo/'
    mavenRepo name: 'Bintray Asciidoctor repo', url: 'http://dl.bintray.com/content/aalmiray/asciidoctor'
    mavenRepo name: 'Bintray JCenter', url: 'http://jcenter.bintray.com'
  }

  dependencies {
    classpath 'org.asciidoctor:asciidoctor-gradle-plugin:0.5.0'
    classpath 'eu.appsatori:gradle-fatjar-plugin:0.2-rc1'
  }

}

ext.redshiftPostgresVersion = '8.0-318.jdbc3'
ext.hadoopVersion = "1.2.1"
ext.cascadingVersion = '2.2.1'
ext.cascadingJDBCVersion = '2.2.0-wip-+'
ext.slf4jVersion = '1.7.2'

apply plugin: 'idea'
apply plugin: 'eclipse'
apply from: "${rootDir}/etc/s3Upload.gradle"

subprojects {
  apply plugin: 'java'
  apply plugin: 'asciidoctor'
  apply plugin: 'idea'
  apply plugin: 'eclipse'
  apply plugin: 'fatjar'

  repositories {
    mavenLocal()
    mavenCentral()
    mavenRepo name: 'conjars', url: 'http://conjars.org/repo/'
    mavenRepo name: 'Bintray Asciidoctor repo', url: 'http://dl.bintray.com/content/aalmiray/asciidoctor'
    mavenRepo name: 'Bintray JCenter', url: 'http://jcenter.bintray.com'
  }

  dependencies {

    compile group: 'cascading', name: 'cascading-jdbc-redshift', version: cascadingJDBCVersion
    compile group: 'postgresql', name: 'postgresql', version: redshiftPostgresVersion

    compile group: 'cascading', name: 'cascading-core', version: cascadingVersion
    compile group: 'cascading', name: 'cascading-hadoop', version: cascadingVersion
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion

    compile( group: 'org.apache.hadoop', name: 'hadoop-core', version: hadoopVersion ) {
      exclude group: 'ant'
      exclude group: 'junit'
      exclude group: 'hsqldb'
      exclude group: 'oro'
    }

  }

  asciidoctor {

    //sourceDocumentName = file( 'src/asciidoc/index.adoc' )

    // http://asciidoc.org/userguide.html#X88
    // attributed must all be lowercase

    def attributes = [
            toc2: '',
            //revnumber: version,
            'source-highlighter': 'highlightjs',
            'highlightjs-theme': 'zenburn',
            'linkcss!': '',
            stylesdir: "src/site/stylesheets/",
            stylesheet: 'cascading.css',
            'data-uri': '',
            imagesdir: "${project.name}/src/site/images/",
            //artifactsurl: project.s3Bucket,
            //docsurl: project.s3BucketDocs,
            //projectroot: project.rootDir,
            analyticsid: System.properties[ 'analytics.id' ],
            analyticsdomain: System.properties[ 'analytics.domain' ]

            // disabled till attributes in docinfo is supported
  //          docinfo1: '',
    ]
    options = [
            template_dir: 'src/templates/',
            eruby: 'erubis',
            attributes: attributes
    ]
  }

  task zipDocs( type: Zip, dependsOn: asciidoctor ){
    baseName = "${project.name}"
    from (asciidoctor.outputDir){
        include '*.*'
    }
    into "${project.name}"
  }


  task docDist( type: Copy, dependsOn: [ asciidoctor, zipDocs ] ) {
    description = "publish documentation"

    from asciidoctor.outputDir
    from zipDocs.archivePath
    into "${rootProject.s3UploadDocs.source}/${project.name}"

    rootProject.s3UploadDocs.dependsOn docDist
  }
}
